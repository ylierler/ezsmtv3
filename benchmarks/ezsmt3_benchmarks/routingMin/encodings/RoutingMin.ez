cspvar(at(X),0,T) :- node(X), critical(Y,T).

cspdomain(fd).

% reachtime is >= 0 
required(at(X)>=0) :- node(X).

% initial node is reached at 0.
reach(X) :- init(X).
required(at(X)==0) :- init(X).

% nodes reached later than the delay
reach(Y) :- reach(X), route(X,Y). 
required(at(Y)-at(X)>=D) :- route(X,Y), edge(X,Y,D).

% critical nodes have to be reached in time
:- critical(X,T), not reach(X).
required(at(X)<=T):- critical(X,T), reach(X).

% any edge can be in the route
{route(X,Y)} :- edge(X,Y,D). 

% one incoming/outgoing edge for each node
:- route(X,Y1), route(X,Y2), node(X), node(Y1), node(Y2), Y1!=Y2. 
:- route(X1,Y), route(X2,Y), node(Y), node(X1), node(X2), X1!=X2.



